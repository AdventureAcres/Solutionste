apply plugin: 'com.android.application'
apply plugin: 'com.google.osdetector'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    //noinspection GroovyAssignabilityCheck
    defaultConfig {
        applicationId "com.proxy.shadowsocksr"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 11
        versionName "1.1.0 Test"
    }
    //noinspection GroovyAssignabilityCheck
    buildTypes {
        debug {
            ndk {
                moduleName "Jni"
                stl "stlport_static"
                abiFilters "armeabi-v7a", "x86"
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            ndk {
                moduleName "Jni"
                stl "stlport_static"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release_arm {
            ndk {
                moduleName "Jni"
                stl "stlport_static"
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release_intel {
            ndk {
                moduleName "Jni"
                stl "stlport_static"
                abiFilters "x86", "x86_64"
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //noinspection GroovyAssignabilityCheck
    sourceSets {
        //noinspection GroovyAssignabilityCheck
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['libs']
        }
    }

    //noinspection GroovyAssignabilityCheck
    task buildNative(type: Exec, description: 'Compile JNI source') {
        def ndkBuild = getNdkDir() + "/ndk-build"
        if (osdetector.os=="windows") {
            ndkBuild += ".cmd"
        }
        workingDir file('src/main')
        commandLine "$ndkBuild", 'clean',
                'NDK_APP_OUT=../../build/obj',
                'NDK_APP_LIBS_OUT=../../libs',
                'APP_BUILD_SCRIPT=jni/Android.mk',
                'NDK_APPLICATION_MK=jni/Application.mk'
        commandLine "$ndkBuild",
                'NDK_APP_OUT=../../build/obj',
                'NDK_APP_LIBS_OUT=../../libs',
                'APP_BUILD_SCRIPT=jni/Android.mk',
                'NDK_APPLICATION_MK=jni/Application.mk',
                '-j', Runtime.runtime.availableProcessors()
        //after compile please manually move bin to assets.
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    //noinspection GroovyAssignabilityCheck
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null) return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    //noinspection GroovyAssignabilityCheck
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    return ndkdir
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    //Crypt Storage
    compile 'com.github.orhanobut:hawk:1.19'
    //Shell
    compile 'eu.chainfire:libsuperuser:1.0.0.+'
    //generate qr code
    compile 'net.glxn.qrgen:android:2.0'
    //DNS Resolve
    compile files('libs/org.xbill.dns_2.1.7.jar')
    //Crypto
    compile 'com.madgag.spongycastle:core:1.53.0.0'
    //mem leak check
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    //exception upload
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
}
